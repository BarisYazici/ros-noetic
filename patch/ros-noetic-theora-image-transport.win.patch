diff --git a/CMakeLists.txt b/CMakeLists.txt
index 4082eac..296f27b 100644
--- a/CMakeLists.txt
+++ b/CMakeLists.txt
@@ -8,11 +8,10 @@ add_message_files(DIRECTORY msg FILES Packet.msg)
 
 generate_messages(DEPENDENCIES std_msgs)
 
-find_package(PkgConfig)
-pkg_check_modules(PC_OGG REQUIRED ogg)
-pkg_check_modules(PC_THEORA REQUIRED theora)
-pkg_check_modules(PC_THEORAENC REQUIRED theoraenc)
-pkg_check_modules(PC_THEORADEC REQUIRED theoradec)
+list(APPEND CMAKE_MODULE_PATH ${CMAKE_CURRENT_LIST_DIR}/cmake)
+
+find_package(OGG)
+find_package(THEORA)
 
 
 # generate the dynamic_reconfigure config file
@@ -27,41 +26,25 @@ catkin_package(
 include_directories(include
   ${catkin_INCLUDE_DIRS}
   ${OpenCV_INCLUDE_DIRS}
-  ${PC_OGG_INCLUDE_DIRS}
-  ${PC_THEORA_INCLUDE_DIRS}
-  ${PC_THEORAENC_INCLUDE_DIRS}
-  ${PC_THEORADEC_INCLUDE_DIRS}
-)
-
-link_directories(${PC_OGG_LIBRARY_DIRS} 
-                 ${PC_THEORA_LIBRARY_DIRS}
-                 ${PC_THEORAENC_LIBRARY_DIRS}
-                 ${PC_THEORADEC_LIBRARY_DIRS})
-add_definitions(${PC_OGG_CFLAGS_OTHER} 
-                ${PC_THEORA_CFLAGS_OTHER}
-                ${PC_THEORAENC_CFLAGS_OTHER}
-                ${PC_THEORADEC_CFLAGS_OTHER}
+  ${OGG_INCLUDE_DIRS}
+  ${THEORA_INCLUDE_DIRS}
 )
 
 add_library(${PROJECT_NAME} src/theora_publisher.cpp src/theora_subscriber.cpp src/manifest.cpp)
 add_dependencies(${PROJECT_NAME} ${PROJECT_NAME}_gencfg ${PROJECT_NAME}_gencpp)
 target_link_libraries(${PROJECT_NAME} ${catkin_LIBRARIES}
                                       ${OpenCV_LIBRARIES}
-                                      ${PC_OGG_LIBRARIES}
-                                      ${PC_THEORA_LIBRARIES}
-                                      ${PC_THEORAENC_LIBRARIES}
-                                      ${PC_THEORADEC_LIBRARIES}
+                                      ${OGG_LIBRARIES}
+                                      ${THEORA_LIBRARIES}
 )
 
 class_loader_hide_library_symbols(${PROJECT_NAME})
 
 add_executable(ogg_saver src/ogg_saver.cpp)
-target_link_libraries(ogg_saver ${PC_THEORA_LIBRARY} 
-                                ${PC_OGG_LIBRARY} 
+target_link_libraries(ogg_saver ${OGG_LIBRARIES} 
                                 ${OpenCV_LIBRARIES} 
                                 ${catkin_LIBRARIES}  
-                                ${PC_THEORAENC_LIBRARIES}
-                                ${PC_THEORADEC_LIBRARIES})
+                                ${THEORA_LIBRARIES})
 add_dependencies(ogg_saver ${PROJECT_NAME}_gencpp)
 
 install(TARGETS ${PROJECT_NAME}
diff --git a/theora_image_transport/cmake/FindOGG.cmake b/theora_image_transport/cmake/FindOGG.cmake
index e69de29..3121d17 100644
--- a/cmake/FindOGG.cmake
+++ b/cmake/FindOGG.cmake
@@ -0,0 +1,26 @@
+find_path(OGG_INCLUDE_DIR
+  NAMES
+    ogg/ogg.h
+  DOC "ogg include directory")
+mark_as_advanced(OGG_INCLUDE_DIR)
+
+find_library(OGG_LIBRARY
+  NAMES
+    ogg
+  DOC "ogg library")
+mark_as_advanced(OGG_LIBRARY)
+
+include(FindPackageHandleStandardArgs)
+find_package_handle_standard_args(OGG REQUIRED_VARS OGG_LIBRARY OGG_INCLUDE_DIR)
+
+if (OGG_FOUND)
+  set(OGG_LIBRARIES "${OGG_LIBRARY}")
+  set(OGG_INCLUDE_DIRS "${OGG_INCLUDE_DIR}")
+
+  if (NOT TARGET OGG::OGG)
+    add_library(OGG::OGG UNKNOWN IMPORTED)
+    set_target_properties(OGG::OGG PROPERTIES
+      IMPORTED_LOCATION "${OGG_LIBRARY}"
+      INTERFACE_INCLUDE_DIRECTORIES "${OGG_INCLUDE_DIR}")
+  endif ()
+endif ()
diff --git a/theora_image_transport/cmake/FindTHEORA.cmake b/theora_image_transport/cmake/FindTHEORA.cmake
index e69de29..73205cf 100644
--- a/cmake/FindTHEORA.cmake
+++ b/cmake/FindTHEORA.cmake
@@ -0,0 +1,67 @@
+include(CMakeFindDependencyMacro)
+
+find_path(THEORA_INCLUDE_DIR
+  NAMES
+    theora/theora.h
+  DOC "theora include directory")
+mark_as_advanced(THEORA_INCLUDE_DIR)
+
+get_filename_component(computed_theora_root "${THEORA_INCLUDE_DIR}" DIRECTORY)
+
+find_library(THEORA_LIBRARY
+  NAMES
+    theora
+  HINTS
+    "${computed_theora_root}/lib"
+    "${computed_theora_root}/lib64"
+  DOC "theora library")
+mark_as_advanced(THEORA_LIBRARY)
+
+find_library(THEORA_enc_LIBRARY
+  NAMES
+    theoraenc
+  HINTS
+    "${computed_theora_root}/lib"
+    "${computed_theora_root}/lib64"
+  DOC "theora encoding library")
+mark_as_advanced(THEORA_enc_LIBRARY)
+
+find_library(THEORA_dec_LIBRARY
+  NAMES
+    theoradec
+  HINTS
+    "${computed_theora_root}/lib"
+    "${computed_theora_root}/lib64"
+  DOC "theora decoding library")
+mark_as_advanced(THEORA_dec_LIBRARY)
+
+include(FindPackageHandleStandardArgs)
+find_package_handle_standard_args(THEORA
+  REQUIRED_VARS THEORA_LIBRARY THEORA_enc_LIBRARY THEORA_dec_LIBRARY THEORA_INCLUDE_DIR)
+
+if (THEORA_FOUND)
+  set(THEORA_LIBRARIES "${THEORA_LIBRARY}" "${THEORA_enc_LIBRARY}" "${THEORA_dec_LIBRARY}")
+  set(THEORA_INCLUDE_DIRS "${THEORA_INCLUDE_DIR}")
+
+  if (NOT TARGET THEORA::THEORA)
+    add_library(THEORA::THEORA UNKNOWN IMPORTED)
+    set_target_properties(THEORA::THEORA PROPERTIES
+      IMPORTED_LOCATION "${THEORA_LIBRARY}"
+      INTERFACE_INCLUDE_DIRECTORIES ${THEORA_INCLUDE_DIR})
+  endif ()
+
+  if (NOT TARGET THEORA::ENC)
+    add_library(THEORA::ENC UNKNOWN IMPORTED)
+    set_target_properties(THEORA::ENC PROPERTIES
+      IMPORTED_LOCATION "${THEORA_enc_LIBRARY}"
+      INTERFACE_INCLUDE_DIRECTORIES ${THEORA_INCLUDE_DIR})
+  endif()
+
+  if (NOT TARGET THEORA::DEC)
+    add_library(THEORA::DEC UNKNOWN IMPORTED)
+    set_target_properties(THEORA::DEC PROPERTIES
+      IMPORTED_LOCATION "${THEORA_dec_LIBRARY}"
+      INTERFACE_INCLUDE_DIRECTORIES ${THEORA_INCLUDE_DIR})
+  endif()
+endif ()
+
